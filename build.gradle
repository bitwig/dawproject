plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id "com.diffplug.spotless" version "6.25.0"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

// Ignore Alpha and Beta versions when checking for version updates
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

spotless {
    java {
        target 'src/**/*.java'
        targetExclude 'src/**/module-info.java'
        importOrder('java', 'javax', 'jakarta', '')
        removeUnusedImports()
        eclipse()
    }
}

version = '1-SNAPSHOT'
group = 'com.bitwig.open'
description = 'dawproject file format'
buildDir = new File(project.projectDir, "target")

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

dependencies {
    implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '4.0.2'
    runtimeOnly group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '4.0.5'
    implementation group: 'commons-io', name: 'commons-io', version: '2.18.0'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'

    annotationProcessor group: 'com.github.therapi', name: 'therapi-runtime-javadoc-scribe', version: '0.15.0'
    implementation group: 'com.github.therapi', name: 'therapi-runtime-javadoc', version: '0.15.0'
    testImplementation group: 'com.j2html', name: 'j2html', version: '1.6.0'
    testImplementation group: 'org.reflections', name: 'reflections', version: '0.10.2'
}
